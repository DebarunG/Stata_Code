clear
cap log close
set more off

log using LimitedDepVar.log, text replace

* Using FRINGE.csv to analyse the relationship between pension benefit amount and-
* characteristics such as age, sex, gender etc

import delimited "https://raw.githubusercontent.com/DebarunG/Stata_data/main/FRINGE.csv", delimiter(comma) clear

*summary statistics for pension
su pension
count if pension ==0
su pension if pension==0
su pension if pension>0
histogram pension, bin(50)

*tobit model 
tobit pension exper age tenure educ depends married white male, ll(0) 
margins, dydx(*) predict(ystar(0,.))

*Examining the difference in benefits between white males and nonwhite females
*Prediction white male (prediction of the latent variable y* which is continuous)
margins,  at(exper=10 age=35 tenure =10 edu=16 depends=0 married=0 white=1 male=1) 
*Prediction nonwhite female
margins,  at(exper=10 age=35 tenure =10 edu=16 depends=0 married=0 white=0 male=0)

*NOTE: the coefficients must be plugged into the equation to get the value of x(beta_hat)
*then x(beta_hat) needs to be used along with the esimated standard error in-
*order to calculate the actual difference in benefits

*compute expected pension for white male

matrix X=(10, 35, 10, 16, 0, 0, 1, 1, 1)
matrix beta=e(b)'
scalar sig = beta[10,1]
matrix beta=beta[1..9,1..1]
matrix Xb=X*beta
scalar xb = el(Xb,1,1)
scalar z=xb/sig
scalar P_w = normal(z)*xb+sig*normalden(z)

* compute expected pension for nonwhite female

matrix X=(10, 35, 10, 16, 0, 0, 0, 0, 1)
matrix beta=e(b)'
scalar sig = beta[10,1]
matrix beta=beta[1..9,1..1]
matrix Xb=X*beta
scalar xb = el(Xb,1,1)
scalar z=xb/sig
scalar P_nw = normal(z)*xb+sig*normalden(z)

* difference in expected pensions
di P_w-P_nw

*tobit with pension-earnings ratio as the dependent variable
tobit peratio exper age tenure educ depends married white male union, ll(0)

*test for joint significance
test white male

clear
log close




